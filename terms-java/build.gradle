plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'network.mysterium'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    testImplementation(
        platform('org.junit:junit-bom:5.7.1'),
        'org.junit.jupiter:junit-jupiter'
    )
}

sourceSets {
    main {
        resources {
            srcDirs 'src/main/resources', '../documents'
        }
    }
}

def mavenToken = System.getenv('MAVEN_TOKEN')
def nextVersion = System.getenv('NEXT_VERSION').replace('v', '')

println("Next version: $nextVersion")
println project.version

processResources {
    filesMatching('**/build.properties') {
        filter {
            it.replace('@project.version@', nextVersion ?: project.version)
        }
    }
}

publishing {
    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = "${System.env.MAVEN_USER}"
                password = "${System.env.MAVEN_PASS}"
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = 'network.mysterium'
            artifactId = 'terms'
            version = nextVersion ?: project.version
            from components.java

            pom {
                name = 'Mysterium Network Terms'
                description = 'A Mysterium Network Terms bundled as maven depdenceny'
                url = 'https://github.com/mysteriumnetwork/terms.git'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/mysteriumnetwork/terms/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'MysteriumTeam'
                        name = 'Mysterium Team'
                        email = 'core-services@mysterium.network'
                        url = 'https://mysterium.network/about/team/'
                        organization = 'Mysterium Network'
                        organizationUrl = 'https://mysterium.network'
                    }
                }
                scm {
                    connection = 'https://github.com/mysteriumnetwork/terms.git'
                    developerConnection = 'https://github.com/mysteriumnetwork/terms.git'
                    url = 'https://github.com/mysteriumnetwork/terms'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
